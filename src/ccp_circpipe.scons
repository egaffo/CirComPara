'''
This SConscript characterize the transcriptome from one RNA-seq sample.
It composes a pipeline that gives statistics on the raw data and
also processes the raw reads to map them on a reference genome, 
reconstruct transcripts, estimates gene/transcript expression, and 
also detects circRNAs by means of backsplice junctions.

Software dependencies are inherited from the CIRCOMPARA-SConscripts used:
 * ccp_read_statistics.scons
 * ccp_preprocess.scons
 * ccp_hisat2.scons
 * ccp_expression.scons
 * ccp_circrna_methods.scons

When called from a SConscript it imports the following variables:
 * env_circpipe

'''

import os, itertools, re

## SET (DISPATCHER) SCRIPT NAMES. THESE ARE THE MAIN PIPELINE STEPS, EACH OF THEM MIGHT FIRE
## DIFFERENT ACTIONS/SCONSCRIPTS
ccp_read_statistics = 'ccp_read_statistics.scons'
ccp_preprocess      = 'ccp_preprocess.scons'
ccp_mapping         = 'ccp_hisat2.scons'
ccp_expression      = 'ccp_expression.scons'
ccp_circrna_methods        = 'ccp_circrna_methods.scons'

Import('*')

try:
    env = env_circpipe.Clone()
except NameError:
    varfile = ARGUMENTS.get('VARS', 'vars.py')
    vars = Variables(varfile)
    vars.Add('CPUS', 'Set number of CPUs', '4')
    vars.Add('PREPROCESSOR', 'The preprocessing method', 'trimmomatic')
    vars.Add('PREPROCESSOR_PARAMS', 
             '''Read preprocessor extra parameters. F.i. if Trimmomatic, an empty string '''\
             '''defaults to '''\
             '''MAXINFO:40:0.5 LEADING:20 TRAILING:20 SLIDINGWINDOW:4:30 MINLEN:50 AVGQUAL:30 ''', 
             '')
    vars.Add('ADAPTER_FILE', 'FASTA file full path of the adapter sequence', '')
    vars.Add('ANNOTATION', 'Gene annotation (Ensembl GFF)', '')
    vars.Add('GENOME_FASTA', 'The FASTA file with the reference genome', 'genome.fa')
    vars.Add('GENOME_INDEX', '''The index of the reference genome for HISAT2''', '/path/to/index')
    vars.Add('READS', 'RNA-seq reads. Comma separated list if paired-end', 'reads.fa')

    vars.Add('SEGEMEHL_INDEX', '''The .idx index for segemehl''', 'genome.idx')
    vars.Add('BWA_INDEX', '''The index of the reference genome for BWA''','/path/to/index')
    vars.Add('BWA_PARAMS','Extra parameters for BWA','')
    #vars.Add('CIRI', 'The full path to the CIRI_vx.x.pl perl script', '')
    vars.Add('BOWTIE2_INDEX', '''The index of the reference genome for BOWTIE2''','/path/to/index')

    vars.Add('STAR_INDEX', 'The directory path where to find Star genome index', 
             '/path/to/Star/index/dir')
    vars.Add('GENEPRED', 'The genome annotation in GenePred format', 'genes.genePred')
    vars.Add('HISAT2_EXTRA_PARAMS', '''Extra parameters to add to the HISAT2 aligner fixed '''\
             '''parameters '--dta --dta-cufflinks --rg-id <SAMPLE> --no-discordant '''\
             '''--no-mixed --no-overlap'. For instance, '--rna-strandness FR' if stranded reads'''\
             ''' are used.''', '')
    vars.Add('CUFFLINKS_PARAMS', '''Cufflinks extra parameters. '''\
             '''F.i. '--library-type fr-firststrand' if dUTPs stranded library were used '''\
             '''for the sequencing''', '')
    vars.Add('CIRI_EXTRA_PARAMS', 'CIRI additional parameters', '')
    vars.Add('CIRCRNA_METHODS', 'Comma separated list of circRNA detection methods to use. '\
	     'Use all methods available as default', '')
    vars.Add('TOGGLE_TRANSCRIPTOME_RECONSTRUCTION', 'Set True to enable transcriptome '\
	     'reconstruction. Default only quantifies genes and transcripts from the given '\
	     'annotation GTF file', 'False')
    vars.Add('BYPASS', 'Skip analysis of linear/circular transcripts. This will also skip '\
	     'the analysis of linear-to-circular expression correlation.'\
         '{linear,circular}',
	     'False')

    env = Environment(variables = vars,
                      ENV = os.environ)
    Help(vars.GenerateHelpText(env))
    unknown = vars.UnknownVariables()
    if unknown:
        print "Run sample: unknown variables", unknown.keys()
        Exit(1)

    if env['TOGGLE_TRANSCRIPTOME_RECONSTRUCTION'].tolower() == 'true':
    	env.Replace(TOGGLE_TRANSCRIPTOME_RECONSTRUCTION = True)
    else:
    	env.Replace(TOGGLE_TRANSCRIPTOME_RECONSTRUCTION = False)

    env.Replace(READS = env['READS'].split(','))
    env.Replace(CIRCRNA_METHODS = env['CIRCRNA_METHODS'].strip().split(','))

SRC_DIR = os.path.join(env['ENV']['CIRCOMPARA_HOME'], 'src')

results = []

## GET INPUT READ FILE FULL PATH
raw_reads = [File(f).abspath for f in env['READS']]

## COMPUTE STATISTICS ON RAW READS
read_statistics_dir = 'read_statistics'
raw_read_stats = []
for readset in raw_reads:
    read_statistics_readset = readset
    stat = SConscript(os.path.join(read_statistics_dir, ccp_read_statistics), 
                      src_dir=SRC_DIR, variant_dir=read_statistics_dir, duplicate=0,
                      exports='env read_statistics_readset')
    raw_read_stats.append(stat)

results.append(raw_read_stats)

build_dir = 'processings'
## EXECUTE SCRIPTS: 
## READ PREPROCESSING
preprocess_raw_reads    = raw_reads
preprocess_adapter_file = env['ADAPTER_FILE']
preprocess_preprocessor = env['PREPROCESSOR'] 
preprocess_cpus         = env['CPUS']
preprocess_params = env['PREPROCESSOR_PARAMS']
preprocess  = env.SConscript(os.path.join(build_dir, ccp_preprocess), 
                             variant_dir = build_dir, src_dir = SRC_DIR, 
                             duplicate = 0, exports='env preprocess_raw_reads '''
                             '''preprocess_adapter_file preprocess_preprocessor '''
                             '''preprocess_cpus preprocess_params''')
results.append(preprocess)
## set the clean read file paths, relative to the calling SConstruct (not the calling SConscript).
## Implementation note: here you should know what the preprocess SConscript returns to handle it 
## properly. For instance: Trimmomatic with paired-end reads gives four output read files and 
## you probably want to use the 0 and 2 indexed files of the returned list (i.e. the processed 
## reads with mate read). Instead, if no preprocessing was performed, the clean reads are the raw
## reads and the list is only two elements (for paired-end reads)

## get first in pair or the single end preprocessed read file
clean_reads = [preprocess[0][0]]
## paired end case: add the second in pair clean read file
if len(preprocess[0]) > 2:
    ## case: Trimmomatic paired-end
    if env['PREPROCESSOR']=='trimmomatic':
        clean_reads.append(preprocess[0][2])
    ## case: no preprocessing was performed
    else:
        clean_reads.append(preprocess[0][1])

sample_name = env['SAMPLE']

## ALIGN TO GENOME
hisat2_index = env['GENOME_INDEX']
sample_id = sample_name
hisat2_extra_params = env['HISAT2_EXTRA_PARAMS'] + ' --dta --dta-cufflinks --rg-id ' + sample_name +\
                      ' --no-discordant --no-mixed --no-overlap '
hisat2_cpus = env['CPUS']
reads_to_map = clean_reads
mappings    = env.SConscript(os.path.join(build_dir, ccp_mapping), 
                             variant_dir = build_dir, src_dir = SRC_DIR, 
                             duplicate = 0, exports = '''env reads_to_map '''
                             '''hisat2_index sample_id hisat2_extra_params '''
                             '''hisat2_cpus''')
results.append(mappings)

mapping_file = mappings[0]
env['ALIGNMENTS'] = mappings[0]

if not env['BYPASS'] == 'linear':
    ## EXPRESSION ANALYSIS
    expression_cpus = env['CPUS']
    expression_genome_fasta = env['GENOME_FASTA'] 
    expression_annotation = env['ANNOTATION']
    expression_cufflinks_params = env['CUFFLINKS_PARAMS']
    expression_toggle_transcriptome_reconstruction = env['TOGGLE_TRANSCRIPTOME_RECONSTRUCTION']
    env['METHODS'] = env['LINEAR_EXPRESSION_METHODS']
    
    expression  = env.SConscript(os.path.join(build_dir, ccp_expression), 
                                 variant_dir = build_dir, src_dir = SRC_DIR, 
                                 duplicate = 0, exports = '''env mapping_file '''
                                 '''sample_name expression_cpus expression_genome_fasta '''
                                 '''expression_annotation expression_cufflinks_params '''
                                 '''expression_toggle_transcriptome_reconstruction''')

    if 'stringtie' in env['METHODS']:
        results.append(expression[1]['STRINGTIE']['ALL_TARGETS'])
        # the ALL_TARGETS is a temporary hack. It will be removed when SConscript
        # results are passed as dictionaries/custom-objects and handled accordingly
    if 'htseq' in env['METHODS']:
        results.append(expression[1]['HTSEQ'])
    if 'cufflinks' in env['METHODS']:
        results.append(expression[1]['CUFFLINKS'])

else:
	expression = env.Command('mock_expression.txt', mappings, 'touch $TARGET')

## CIRCRNA DETECTION
unmapped_dir = os.path.join(build_dir, 'unmapped_reads')

if not env['BYPASS'] == 'circular':
    ## Extract unmapped reads in FASTQ format from BAM alignments
    unmapped_reads_target = ['singleton.fastq.gz']
    #unmapped_reads_cmd = '''samtools fastq -f 12 -F 3328 -n -s >( gzip -c > ${TARGETS[0]} ) '''
    unmapped_reads_cmd = '''samtools fastq -f 12 -F 3328 -n -s ${str(TARGETS[0].abspath).replace('.gz', '')}  '''
    #SAMFLAG 3328 = 256+1024+2048
    #4: read unmapped
    #8: mate unmapped
    #256: not primary alignment
    #1024: read is PCR or optical duplicate
    #2048: supplementary alignment
    # samtools fastq:
    # -s FILE   write singleton reads to FILE [assume single-end]
    # -n        don't append /1 and /2 to the read name
    if len(env['READS']) > 1:
        paired_end_read_files = ['unmapped_1.fastq.gz', 'unmapped_2.fastq.gz']
        unmapped_reads_target.extend(paired_end_read_files)
        unmapped_reads_cmd = unmapped_reads_cmd +\
                             ''' -1 ${str(TARGETS[1].abspath).replace('.gz', '')} '''\
                             ''' -2 ${str(TARGETS[2].abspath).replace('.gz', '')} ${SOURCE} '''\
                             '''&& gzip ${str(TARGETS[1].abspath).replace('.gz', '')} '''\
                             '''&& gzip ${str(TARGETS[2].abspath).replace('.gz', '')} '''
    #                         ''' -1 >( gzip -c > ${TARGETS[1]} )'''\
    #                         ''' -2 >( gzip -c > ${TARGETS[2]} ) ${SOURCE}'''
        unmapped_reads_cmd = unmapped_reads_cmd +\
                            ''' && gzip ${str(TARGETS[0].abspath).replace('.gz', '')}'''
        
        unmapped_reads = env.Command([os.path.join(unmapped_dir, f) for f in unmapped_reads_target], 
                                     mapping_file.abspath, 
                                     unmapped_reads_cmd)
        
    else:
        #unmapped_reads_target.append('unmapped.fastq.gz')
        #unmapped_reads_cmd = unmapped_reads_cmd + ''' ${SOURCE} | gzip -c > ${TARGETS[1]}'''
        unmapped_reads = mappings[2]
    
    results.append(unmapped_reads)
                        
    # run circRNA detection pipeline 
    sample_cpus         = env['CPUS']
    sample_genome_fasta = env['GENOME_FASTA']
    sample_annotation   = env['ANNOTATION']
    
    if len(env['READS']) > 1:
        sample_raw_reads    = [f.abspath for f in unmapped_reads[1:3]]
    else:
        sample_raw_reads    = [unmapped_reads.abspath]
    
    sample_segemehl_index     = env['SEGEMEHL_INDEX']
    ciri_bwa_index            = env['BWA_INDEX']
    ciri_bwa_extra_parameters = env['BWA_PARAMS']
    ciri_script               = env['CIRI']
    
    bowtie2_index = env['BOWTIE2_INDEX']
    
    star_index  = env['STAR_INDEX']
    gene_pred   = env['GENEPRED']
    
    ## set strandness parameter for circRNA detection methods
    ## check by HISAT parameters
    strandness_pattern = re.compile("--rna-strandness\s+[FR]{1,2}")
    if strandness_pattern.search(hisat2_extra_params):
        ## Environment is propagated to the Sconscript
        env['FINDCIRC_EXTRA_PARAMS'] = ' --strandpref --stranded '
    
    ## select which circRNA methods have to be performed
    circrna_methods = env['CIRCRNA_METHODS']
    env_sample_circrna_methods = env.Clone()
    env_sample_circrna_methods.Replace(READS = sample_raw_reads)
    ciri_extra_parameters = env['CIRI_EXTRA_PARAMS']
    circrnas = env.SConscript(os.path.join(build_dir, ccp_circrna_methods), 
                            src_dir = SRC_DIR, 
                            variant_dir = build_dir, duplicate = 0,
                            exports = '''env_sample_circrna_methods sample_cpus sample_genome_fasta '''
                            '''sample_annotation sample_raw_reads sample_segemehl_index '''
                            '''ciri_bwa_index ciri_bwa_extra_parameters '''
                            '''ciri_script bowtie2_index star_index gene_pred '''
                            '''ciri_extra_parameters circrna_methods''')
    results.append(circrnas)

#Depends(circrnas, unmapped_reads)

## CLEAN COMMANDS
Clean('.', read_statistics_dir)
Clean('.', build_dir)
Clean('.', unmapped_dir)

Return('results')
