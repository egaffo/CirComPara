import os

Import('*')

try:
    env = env_linear_expression.Clone()
except NameError:
    varfile = ARGUMENTS.get('VARS', 'vars.py')
    vars = Variables(varfile)
    vars.Add('META', '', '')
    vars.Add('LINEAR_ALIGNMENTS', '', '')
    
    env = Environment(variables = vars,
                      ENV = os.environ)
    Help(vars.GenerateHelpText(env))
    unknown = vars.UnknownVariables()
    if unknown:
        print "Unknown variables:", unknown.keys()
        Exit(1)


## QUANTIFY EXPRESSION
gene_exp = ''
gene_meta = ''
#sample = ''

cuffquant_dir = 'cuffquant'
cuffdiff_dir = 'cuffdiff'
htseq_counts_dir = 'htseq_counts'
## the directory where to collect gene expression results
geneexp_dir = 'geneexp'

ballgown_dir = os.path.join(geneexp_dir, 'ballgown')
ballgown_files = []

env['LINEAR_ALIGNMENTS'] = get_matching_nodes(env['RUNS'],
                                              '.*hisat2_out.*\.bam')

if 'stringtie' in env['LINEAR_EXPRESSION_METHODS']:
    #sample = ','.join(env['SAMPLES'].keys())
    gene_meta = env['META']
    if env['TOGGLE_TRANSCRIPTOME_RECONSTRUCTION']:
        ## (RE)COMPUTE GENE ABUNDANCES USING MERGED ANNOTATION
        gene_exp = []
        for sample_name in env['SAMPLES'].keys():
            
            alignment = get_matching_nodes(env['LINEAR_ALIGNMENTS'], '.*' + sample_name + '.*')

            if len(alignment) > 1: 
                print 'Warning: multiple alignments matched for sample ' +\
                      sample_name + '. Will use only the first: ' +\
                      alignment[0].path

            alignment = alignment[0]
            
            gene_ab_targets = [sample_name + '_transcripts.gtf', 
                               sample_name + '_gene_abund.tab',
                               sample_name + '_cov_refs.gtf']
            
            gene_ab_targets = [os.path.join(geneexp_dir, sample_name, t) for t in gene_ab_targets]

            gene_ab_targets.extend([os.path.join(ballgown_dir, 
                                                 sample_name,
                                                 tab) for tab in ['e2t.ctab', 
                                                                'e_data.ctab',  
                                                                'i2t.ctab',  
                                                                'i_data.ctab',  
                                                                't_data.ctab']]
                                    )
            
            gene_ab_cmd = 'stringtie $( -p $CPUS $) -e -G ${SOURCES[1]} -o ${TARGETS[0]} '\
                          '-A ${TARGETS[1]} -C ${TARGETS[2]} -b ${TARGETS[3].dir} ${SOURCES[0]}' 
            gene_ab = env.Command(gene_ab_targets, 
                                  [alignment, env['ANNOTATION']], 
                                  gene_ab_cmd)
            gene_exp.append(gene_ab[1].abspath)
            ballgown_files.append(gene_ab[3:])

    else:
        ## JUST COLLECT GENE ABUNDANCES ALREADY COMPUTED WITH GIVEN ANNOTATION
        # TODO: use finer way to collect files, by grubbing dictionary results from the runs 
        # gene_exp = []
        # sample_list = []
        # for sample_name in env['SAMPLES'].keys():
        #     gene_exp.append(env['RUNS'][sample_name]['LINEAR_EXPRESSION']['STRINGTIE']['GENE_ABUND'])
        #     sample_list.append(sample_name)
        # sample = ','.join(sample_list)
        # TODO: collect ballgown_files
        gene_exp = get_matching_nodes(env['RUNS'], '.*gene_abund\.tab')
        trx_exp = get_matching_nodes(env['RUNS'], '.*_transcripts\.gtf')
        ballgown_files = get_matching_nodes(env['RUNS'], '.*\.ctab')
        gene_raw_counts_list = []
        trx_raw_counts_list = []
        for sample_name in env['SAMPLES'].keys():
            raw_counts = env['RUNS_DICT'][sample_name]['LINEAR_EXPRESSION']['STRINGTIE']['RAW_COUNTS']
            gene_raw_counts_list.append(raw_counts[0])
            trx_raw_counts_list.append(raw_counts[1])

    env.Replace(EXPRESSION_FILES = ballgown_files)

   
if ('cufflinks' in env['LINEAR_EXPRESSION_METHODS']) or \
    (env['DIFF_EXP'] and 'cuffdiff' in env['DIFF_EXP']): ## exploit lazy evaluation
    if len(env['SAMPLES'].keys()) > 1:
        cuffquant_annotation = env['ANNOTATION']
        cuffquant_alignments = env['LINEAR_ALIGNMENTS']
        cuffquant_genome_fasta = env['GENOME_FASTA']
        cuffquant_extra_params = env['CUFFQUANT_EXTRA_PARAMS']
        
        cuffquant_cpus = env['CPUS']
        
        cuffquant = SConscript(os.path.join(cuffquant_dir, 'ccp_cuffquant.scons'),
                               src_dir = env['SCONSCRIPT_HOME'],
                               variant_dir = cuffquant_dir, duplicate = 0,
                               exports = '''env cuffquant_alignments cuffquant_annotation '''
                               '''cuffquant_genome_fasta cuffquant_cpus cuffquant_extra_params''')
        Depends(cuffquant, [env['ANNOTATION'], env['LINEAR_ALIGNMENTS']])
        
        ## Normalize expression values: CUFFNORM
        env_cuffnorm = env.Clone()
        env_cuffnorm['QUANT_FILES']= get_matching_nodes(cuffquant, '.*\.cxb')
        env_cuffnorm['EXTRA_PARAMS']= env['CUFFNORM_EXTRA_PARAMS'] #TODO: set strandness when required
        env_cuffnorm['LABELS'] = env['CONDITIONS']
        
        cuffnorm = SConscript(os.path.join(cuffdiff_dir, 'ccp_cuffnorm.scons'),
                              src_dir = env['SCONSCRIPT_HOME'],
                              variant_dir = cuffdiff_dir, duplicate = 0,
                              exports = '''env_cuffnorm''')
        
        Depends(cuffnorm, cuffquant)
        
        gene_exp = get_matching_nodes(cuffnorm, ".*genes.read_group_tracking")
        gene_meta = File(get_matching_nodes(cuffnorm, ".*read_groups.info")[0]).abspath
        trx_exp = get_matching_nodes(cuffnorm, ".*isoforms.read_group_tracking")
        gene_raw_counts_list = gene_exp
        trx_raw_counts_list = trx_exp

    else:
        ## case of only one sample
        gene_exp = get_matching_nodes(env['RUNS'], '.*genes.fpkm_tracking')
        gene_meta = env['META']
        #sample = str(env['SAMPLES'].keys()[0])

        trx_exp = get_matching_nodes(env['RUNS'], ".*isoforms.fpkm_tracking")
        gene_raw_counts_list = gene_exp
        trx_raw_counts_list = trx_exp

    env.Replace(EXPRESSION_FILES = gene_exp)

if 'htseq_count' in env['LINEAR_EXPRESSION_METHODS']:
    ## COMPUTE READ COUNTS FOR EACH ALIGNMENT FILE, ACCORDING TO THE GIVEN ANNOTATION
    htseq_count_annotation = env['ANNOTATION']
    htseq_counts = []
    for algn in get_matching_nodes(env['RUNS'], alignment_matching_regexp):
        sample_name = os.path.splitext(File(algn).name)[0]
        mapping_file = algn
        htseq_count_cpus = env['CPUS']
        htseq_count_stranded = 'no'
        if '--rna-strandness FR' in env['HISAT2_EXTRA_PARAMS']: 
            htseq_count_stranded = 'yes'
        ## GET READ COUNTS
        htseq_count = SConscript(os.path.join(htseq_counts_dir, 'ccp_htseq_count.scons'), 
                                 src_dir = env['SCONSCRIPT_HOME'], 
                                 variant_dir = htseq_counts_dir, duplicate = 0, 
                                 exports = '''env mapping_file sample_name '''
                                           '''htseq_count_annotation htseq_count_cpus '''
                                           '''htseq_count_stranded ''')
        htseq_counts.append(htseq_count)

    env.Replace(EXPRESSION_FILES = htseq_counts)
    ## TODO: fix gene expression report for htseq_count. 
    gene_meta = env['META'] # 
    gene_exp = htseq_counts #

## REPORT GENE EXPRESSION
gene_exp_analysis_template = os.path.join("$SCONSCRIPT_HOME", "gene_expression_analysis.Rmd")
gene_xpr_files = ','.join([str(s.abspath) for s in gene_exp])
transcripts_gtf_files = ','.join([str(s.abspath) for s in trx_exp])
gene_raw_counts = ','.join([str(s.abspath) for s in gene_raw_counts_list])
trx_raw_counts = ','.join([str(s.abspath) for s in trx_raw_counts_list])

gene_exp_analysis_cmd = '''Rscript -e 'results.dir <- dirname("${TARGETS[0].abspath}"); '''\
                        '''meta.file <- "${SOURCES[0].abspath}"; '''\
                        '''gene.xpr.file <- "''' + gene_xpr_files + '''"; '''\
                        '''transcripts.gtf.files <- "''' + transcripts_gtf_files + '''"; '''\
                        '''gene_raw_counts_list <- "''' + gene_raw_counts + '''"; '''\
                        '''trx_raw_counts_list <-"''' + trx_raw_counts + '''"; '''\
                        '''linear.method <- "${ ','.join( LINEAR_EXPRESSION_METHODS ) }"; '''\
                        '''rmarkdown::render(input = "''' + \
                        str(gene_exp_analysis_template) + '''", '''\
                        '''output_file = "$TARGET.abspath", '''\
                        '''intermediates_dir = dirname("${TARGETS[0].abspath}") )' '''

gene_exp_analysis_targets = [os.path.join(geneexp_dir, 
                                          "gene_expression_analysis.html"),
                             os.path.join(geneexp_dir,
                                          "gene_expression_FPKM_table.csv"),
                             os.path.join(geneexp_dir,
                                          "gene_expression_rawcounts_table.csv"),
                             os.path.join(geneexp_dir,
                                         "transcript_expression_rawcounts_table.csv")]
                            # Figs_geneexp

gene_exp_analysis_sources = [gene_meta, gene_exp, trx_exp]
gene_exp_analysis = env.Command(gene_exp_analysis_targets, 
                                gene_exp_analysis_sources, 
                                gene_exp_analysis_cmd)

env['GENE_EXP_ANALYSIS'] = gene_exp_analysis
env['GENE_EXP'] = gene_exp

Clean('.', cuffquant_dir)
Clean('.', cuffdiff_dir)
Clean('.', htseq_counts_dir)
Clean('.', geneexp_dir)

Return('env')
