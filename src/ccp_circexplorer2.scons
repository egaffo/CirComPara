'''
This SConscript detects and annotates circular RNAs from RNA-seq data according to the
CIRCExplorer2 [1] protocol.

Software dependencies are inherited from the CIRCOMPARA-SConscripts used:
* 

When called from a SConscript it imports the following variables:
* circexplorer2_env

References:
1. Zhang, X.-O.; Dong, R.; Zhang, Y.; Zhang, J.-L.; Luo, Z.; Zhang, J.;
Chen, L.-L.; Yang, L. Diverse alternative back-splicing and alternative
splicing landscape of circular RNAs. Genome Res. 2016, 26, 1277-1287,
doi:10.1101/gr.202895.115.
'''

import os 

Import('*')

try:
    env = circexplorer2_env.Clone()

except NameError:
    varfile = ARGUMENTS.get('VARS', 'vars.py')
    vars = Variables(varfile)
    vars.Add('ALIGNER', 'The aligner used to map backsplices. Supported'\
                        'aligners: STAR,BWA,segemehl,TopHat-Fusion,TopHat-Fusion-PE,'\
                        'Mapsplice. Use TopHat-Fusion-PE if paired-end reads.',
             'STAR')
    vars.Add('FUSION_FILE', 'The file to be parsed, depending on the aligner: '\
                            'Chimeric.out.junction for STAR, RNA_seq_bwa.sam '\
                            'for BWA, splicesites.bed for segemehl/testrealign, '\
                            'tophat_fusion/accepted_hits.bam for TopHat-Fusion, '\
                            'mapsplice_out/fusions_raw.txt for Mapsplice', 
            'Chimeric.out.junction')
    vars.Add('GENEPRED', 'The genome annotation in GenePred format', 'genes.genePred')
    vars.Add('GENOME_FASTA', 'The FASTA file with the reference genome', 'genome.fa')
    vars.Add('ALIGNMENTS', 'The alignment file', '')

    env = Environment(variables = vars,
                      ENV = os.environ)
    Help(vars.GenerateHelpText(env))
    unknown = vars.UnknownVariables()
    if unknown:
        print "Run sample: unknown variables", unknown.keys()
        Exit(1)

out_dir = 'CIRCexplorer2_' + env['ALIGNER'].lower()
results = []

env['OPTIONS'] = ''

bks_reads_cmd = '''zcat ${SOURCES[0]} | samtools view -Su - | '''\
                '''bedtools intersect -s -bed -abam stdin -b ${SOURCES[1]} | '''

## use sed to write a BED file with 'single nucleotide' intervals
## NB: stop position is not included in BED format (intervals are 
## 0-based and 'half-open'), so the stop position must be incremented by 1.
## Only the first six tab-separated fields are considered
bed_cmd = '''sed -r 's/([^\\t]+)\\t([^\\t]+)\\t([^\\t]+)\\t'''\
                    '''([^\\t]+)\\t([^\\t]+)\\t([^\\t]+).*/echo "'''\
                    '''\\1\\t\\2\\t$$((\\2+1))\\t\\4\\t\\5\\t\\6\\n'''\
                    '''\\1\\t\\3\\t$$((\\3+1))\\t\\4\\t\\5\\t\\6"/e' '''\
                    '''${SOURCES[0]} | sort -k1,1 -k2,2n -k3,3n > $TARGET'''

#TopHat-Fusion, STAR, MapSplice, BWA, segemel
if env['ALIGNER'].lower() == 'star':
    ALIGNER = 'STAR'    
    bks_reads_cmd = '''bedtools intersect -s -bed -abam ${SOURCES[0]} -b ${SOURCES[1]} | '''
    bks_sfx = ".ce2star"

if env['ALIGNER'].lower() == 'bwa':
    ALIGNER = 'BWA'
    bks_sfx = ".ce2bwa"

if env['ALIGNER'].lower() == 'segemehl':
    ALIGNER = 'segemehl'
    bks_sfx = ".ce2seg"
    ## segemehl coordinates seem to refer to the intron and not the
    ## spliced exons. So, CE2 start position must be increased to 
    ## comply with segemehl SAM.
    ## Moreover, strand seems to be always +
    bed_cmd = '''sed -r 's/([^\\t]+)\\t([^\\t]+)\\t([^\\t]+)\\t'''\
                        '''([^\\t]+)\\t([^\\t]+)\\t([^\\t]+).*/echo "'''\
                        '''\\1\\t$$((\\2+1))\\t$$((\\2+2))\\t\\4\\t\\5\\t\\+\\n'''\
                        '''\\1\\t\\3\\t$$((\\3+1))\\t\\4\\t\\5\\t\\+"/e' '''\
                        '''${SOURCES[0]} | sort -k1,1 -k2,2n -k3,3n > $TARGET'''

if env['ALIGNER'].lower() == 'tophat':
    ALIGNER = 'TopHat-Fusion'
    bks_reads_cmd = '''bedtools intersect -s -bed -abam ${SOURCES[0]} -b ${SOURCES[1]} | '''
    bks_sfx = ".ce2th"

if env['ALIGNER'].lower() == 'tophat_pe':
    ALIGNER = 'TopHat-Fusion'
    env.Replace(OPTIONS = '--pe')
    bks_reads_cmd = '''bedtools intersect -s -bed -abam ${SOURCES[0]} -b ${SOURCES[1]} | '''
    bks_sfx = ".ce2th"

if env['ALIGNER'].lower() == 'mapsplice':
    ALIGNER = 'MapSplice'
    bks_sfx = ".ce2ms"
 
CIRCexplorer2_cmd = 'CIRCexplorer2 parse $OPTIONS -o $TARGET.dir -t ' + \
                    ALIGNER + ' $SOURCE'
CIRCexplorer2_targets = os.path.join(out_dir, 'fusion_junction.bed')
CIRCexplorer2_sources = [File(env['FUSION_FILE'])]
CIRCexplorer2 = env.Command(CIRCexplorer2_targets, 
                            CIRCexplorer2_sources, 
                            CIRCexplorer2_cmd)

## as default, use the fusion_junction.bed file 
## to collect backsplice reads
circ_bed = CIRCexplorer2[0]

results.append(CIRCexplorer2)

if not env['GENEPRED'] == '':
    CIRCexplorer2_annotate_targets = [os.path.join(out_dir, 'annotate', f) 
                                    for f in ['annotated_fusion.txt', 
                                    'circ_fusion.txt']]
    CIRCexplorer2_annotate_sources = [File(env['GENEPRED']),
                                      File(env['GENOME_FASTA']),
                                      CIRCexplorer2[0]]
    CIRCexplorer2_annotate_cmd = 'CIRCexplorer2 annotate -r ${SOURCES[0]} '\
                                '-g ${SOURCES[1]} ${SOURCES[2].dir}'
    CIRCexplorer2_annotate = env.Command(CIRCexplorer2_annotate_targets,
                                        CIRCexplorer2_annotate_sources,
                                        CIRCexplorer2_annotate_cmd)

    ## use the filtered annotate/circ_fusion.txt BED file
    ## to collect backsplice reads
    circ_bed = CIRCexplorer2_annotate[1]

    results.append(CIRCexplorer2_annotate)

    ## NB: VCF are 1-based and BED are 0-based
    #vcf_cmd = '''sed "s/\\t/,/g" ${SOURCES[0]} | sed "s/^/$SAMPLE,/" | '''\
    #          '''convert_circrna_collect_tables.py -p circexplorer2_bwa -f gtf - | '''\
    #          '''cut -f1,4 | sed -E "s/$$/\\t\.\\t\.\\t\.\\t\.\\t\.\\t\./" > ${TARGETS[0]} && '''\
    #          '''sed "s/\\t/,/g" ${SOURCES[0]} | sed "s/^/$SAMPLE,/" | '''\
    #          '''convert_circrna_collect_tables.py -p circexplorer2_bwa -f gtf - | '''\
    #          '''cut -f1,5 | sed -E "s/$$/\\t\.\\t\.\\t\.\\t\.\\t\.\\t\./" >> ${TARGETS[0]} && '''\
    #          '''sort -k1,1 -k2,2n -o ${TARGETS[0]} ${TARGETS[0]}  && '''\
    #          '''sed -i "1i##fileformat=VCFv4.0" ${TARGETS[0]}'''

    #vcf = env.Command([os.path.join(out_dir,
    #                                "${SAMPLE}_circ.vcf")], 
    #                  #[CIRCexplorer2[0]], 
    #                  [CIRCexplorer2_annotate[1]], 
    #                  vcf_cmd)
    #
    #bks_reads_cmd = bks_reads_cmd + \
    #                '''cut -f 4 | sort | uniq -c | '''\
    #                '''sed -E "s/[^0-9]*([0-9]+)[ ]+([^ ]+)[ ]*/\\1\\t\\2/g" | '''\
    #                '''sort -k1,1nr > ${TARGETS[0]} '''
    #
    #bks_reads = env.Command([os.path.join(out_dir, 
    #                                      env['SAMPLE'] + '.ce2.bks.reads')], 
    #                        [env['ALIGNMENTS'], vcf], 
    #                        bks_reads_cmd)

bed = env.Command([os.path.join(out_dir, "${SAMPLE}.sn.circ.bed")],
                  [circ_bed],
                  bed_cmd)

results.append(bed)

bks_reads_cmd = bks_reads_cmd + \
                    '''cut -f 4 | sort | uniq -c | '''\
                    '''sed -E "s/[^0-9]*([0-9]+)[ ]+([^ ]+)[ ]*/\\1\\t\\2/g" | '''\
                    '''sort -k1,1nr > ${TARGETS[0]} '''
    
bks_reads = env.Command([os.path.join(out_dir, 
                                      env['SAMPLE'] + bks_sfx + '.bks.reads')], 
                        [env['ALIGNMENTS'], bed], 
                        bks_reads_cmd)

results.append(bks_reads)

Return('results')

Clean('.', out_dir)

